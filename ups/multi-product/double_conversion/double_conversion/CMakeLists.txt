include(ExternalProject)

# We could just set the CMAKE_INSTALL_*DIR directories directly to
# ${CMAKE_INSTALL_PREFIX}/${double_conversion_*_dir}, but we want to
# use CPack, which only knows about files/directories that are
# install()ed. So we install into a staging area and then install()
# from the staging area into the final destination
ExternalProject_Add (
  double_conversion

  PREFIX         double_conversion
  GIT_REPOSITORY https://github.com/google/double-conversion
  GIT_TAG        v3.1.5
  GIT_SHALLOW    ON

  BUILD_ALWAYS   OFF
  INSTALL_DIR    ${PROJECT_BINARY_DIR}/external_staging
  CMAKE_ARGS     -DCMAKE_INSTALL_PREFIX:PATH=<INSTALL_DIR>
                 -DCMAKE_INSTALL_LIBDIR:PATH=${double_conversion_lib_dir}
                 -DCMAKE_INSTALL_BINDIR:PATH=${double_conversion_bin_dir}
                 -DCMAKE_INSTALL_INCLUDEDIR:PATH=${double_conversion_inc_dir}
                 -DBUILD_SHARED_LIBS=on
)

# The trailing / on the DIRECTORY argument acts like it does for
# cp. We need it to avoid double directories in the output. We have to
# use relative paths for the DESTINATION (which are interpreted as
# being relative to ${CMAKE_INSTALL_PREFIX}), so that CPack will work
message(STATUS "Going to install ${PROJECT_BINARY_DIR}/external_staging to ${CMAKE_INSTALL_PREFIX}")
install(DIRECTORY ${PROJECT_BINARY_DIR}/external_staging/${double_conversion_lib_dir}/
       DESTINATION ${double_conversion_lib_dir})
install(DIRECTORY ${PROJECT_BINARY_DIR}/external_staging/${double_conversion_inc_dir}/
       DESTINATION ${double_conversion_inc_dir})
# The double_conversion-targets.cmake file is generated by the inner cmake (ie, the
# one from double_conversion itself), and contains all the necessary definitions
# that allow other packages to link against/include double_conversion via `double_conversion::double_conversion`
# and similar. We can't generate it ourselves because it's
# autogenerated by cmake based on the targets that are declared in the
# cmake files. We have to copy it because ups puts installed cmake
# files in a non-standard place
install(FILES ${PROJECT_BINARY_DIR}/external_staging/${double_conversion_lib_dir}/cmake/double-conversion/double-conversionTargets.cmake
        DESTINATION ${double_conversion_lib_dir}/double_conversion/cmake/)
